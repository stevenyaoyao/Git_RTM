## ams_version=1.0

Model Main_AIMMS_Scheduling {
    Section Raw_Input {
        Procedure ReadInput {
            Body: {
                empty Raw_Input_Data;
                empty Input_Sets_and_Parameters;
                
                ! Read Sets
                P_FileName := "InputData\\CleanData_v3.xlsx";
                SpreadSheet::SetActiveSheet(Workbook:P_FileName, Name:"Sets");
                numRows := 20;
                dataRangeStr := formatString("D2:D%i",numRows);
                SpreadSheet::RetrieveSet(Workbook:P_FileName, Set:S_VesselType, Range:dataRangeStr, Mode:'replace');
                
                numRows := 200;
                dataRangeStr := formatString("C2:C%i",numRows);
                SpreadSheet::RetrieveSet(Workbook:P_FileName, Set:S_Port, Range:dataRangeStr, Mode:'replace');
                
                numRows := 200;
                dataRangeStr := formatString("B2:B%i",numRows);
                SpreadSheet::RetrieveSet(Workbook:P_FileName, Set:S_Customer, Range:dataRangeStr, Mode:'replace');
                
                numRows := 20;
                dataRangeStr := formatString("A2:A%i",numRows);
                SpreadSheet::RetrieveSet(Workbook:P_FileName, Set:S_FreightMechanism, Range:dataRangeStr, Mode:'replace');
                
                numRows := 20;
                dataRangeStr := formatString("E2:E%i",numRows);
                SpreadSheet::RetrieveSet(Workbook:P_FileName, Set:S_VesselCategory, Range:dataRangeStr, Mode:'replace');
                
                !SpreadSheet::SetActiveSheet(Workbook:P_FileName, Name:"Vessel");
                !numRows := 200;
                !dataRangeStr := formatString("A2:A%i",numRows);
                !SpreadSheet::RetrieveSet(Workbook:P_FileName, Set:S_Vessel, Range:dataRangeStr, Mode:'replace');
                
                ! Read CargoPosition Data
                SpreadSheet::SetActiveSheet(Workbook:P_FileName, Name:"CargoPosition");
                numRows := 200;
                dataRangeStr := formatString("A2:A%i",numRows);
                SpreadSheet::RetrieveSet(Workbook:P_FileName, Set:S_CargoPosition, Range:dataRangeStr, Mode:'replace');
                
                rowRangeStr := formatString("A2:A%i",numRows);
                dataRangeStr := formatString("B2:B%i", numRows);
                SpreadSheet::RetrieveTable(Workbook:P_FileName, Parameter:P_StatusOfCargoPosition, DataRange:dataRangeStr, RowsRange:rowRangeStr);
                
                rowRangeStr := formatString("A2:A%i",numRows);
                dataRangeStr := formatString("C2:C%i", numRows);
                SpreadSheet::RetrieveTable(Workbook:P_FileName, Parameter:P_CustomerOfCargoPosition, DataRange:dataRangeStr, RowsRange:rowRangeStr);
                
                rowRangeStr := formatString("A2:A%i",numRows);
                dataRangeStr := formatString("D2:D%i", numRows);
                SpreadSheet::RetrieveTable(Workbook:P_FileName, Parameter:P_FreightMechanismOfCargoPosition, DataRange:dataRangeStr, RowsRange:rowRangeStr);
                
                rowRangeStr := formatString("A2:A%i",numRows);
                dataRangeStr := formatString("E2:E%i", numRows);
                SpreadSheet::RetrieveTable(Workbook:P_FileName, Parameter:P_FreightRate, DataRange:dataRangeStr, RowsRange:rowRangeStr);
                
                rowRangeStr := formatString("A2:A%i",numRows);
                dataRangeStr := formatString("F2:F%i", numRows);
                SpreadSheet::RetrieveTable(Workbook:P_FileName, Parameter:P_LaycanStartDateOfCargoPosition, DataRange:dataRangeStr, RowsRange:rowRangeStr);
                
                rowRangeStr := formatString("A2:A%i",numRows);
                dataRangeStr := formatString("G2:G%i", numRows);
                SpreadSheet::RetrieveTable(Workbook:P_FileName, Parameter:P_LaycanEndDateOfCargoPosition, DataRange:dataRangeStr, RowsRange:rowRangeStr);
                
                rowRangeStr := formatString("A2:A%i",numRows);
                dataRangeStr := formatString("L2:L%i", numRows);
                SpreadSheet::RetrieveTable(Workbook:P_FileName, Parameter:P_ETATargetOfCargoPosition, DataRange:dataRangeStr, RowsRange:rowRangeStr);
                
                rowRangeStr := formatString("A2:A%i",numRows);
                dataRangeStr := formatString("K2:K%i", numRows);
                SpreadSheet::RetrieveTable(Workbook:P_FileName, Parameter:P_ETBTargetOfCargoPosition, DataRange:dataRangeStr, RowsRange:rowRangeStr);
                
                rowRangeStr := formatString("A2:A%i",numRows);
                dataRangeStr := formatString("N2:N%i", numRows);
                SpreadSheet::RetrieveTable(Workbook:P_FileName, Parameter:P_ETDTargetOfCargoPosition, DataRange:dataRangeStr, RowsRange:rowRangeStr);
                
                rowRangeStr := formatString("A2:A%i",numRows);
                dataRangeStr := formatString("M2:M%i", numRows);
                SpreadSheet::RetrieveTable(Workbook:P_FileName, Parameter:P_CargoStemSizeOfCargoPosition, DataRange:dataRangeStr, RowsRange:rowRangeStr);
                
                rowRangeStr := formatString("A2:A%i",numRows);
                dataRangeStr := formatString("H2:H%i", numRows);
                SpreadSheet::RetrieveTable(Workbook:P_FileName, Parameter:P_CargoTierOfCargoPostion, DataRange:dataRangeStr, RowsRange:rowRangeStr);
                
                rowRangeStr := formatString("A2:A%i",numRows);
                dataRangeStr := formatString("J2:J%i", numRows);
                SpreadSheet::RetrieveTable(Workbook:P_FileName, Parameter:P_LoadingPortOfCargoPosition, DataRange:dataRangeStr, RowsRange:rowRangeStr);
                
                rowRangeStr := formatString("A2:A%i",numRows);
                dataRangeStr := formatString("I2:I%i", numRows);
                SpreadSheet::RetrieveTable(Workbook:P_FileName, Parameter:P_RequireESSOfCargoPositionString, DataRange:dataRangeStr, RowsRange:rowRangeStr);
                
                ! Read PortDistance Data
                SpreadSheet::SetActiveSheet(Workbook:P_FileName, Name:"PortDistance");
                numRows := 500;
                rowRangeStr := formatString("A2:A%i",numRows);
                columnRangeStr := formatString("B1:E1");
                dataRangeStr := formatString("B2:E%i", numRows);
                SpreadSheet::RetrieveTable(Workbook:P_FileName, Parameter:P_Distance, DataRange:dataRangeStr, RowsRange:rowRangeStr, ColumnsRange:columnRangeStr);
                
                ! Read PortVesselCategoryCompability Data
                SpreadSheet::SetActiveSheet(Workbook:P_FileName, Name:"PortVesselCategoryCompatibility");
                numRows := 500;
                rowRangeStr := formatString("A2:A%i",numRows);
                columnRangeStr := formatString("B1:F1");
                dataRangeStr := formatString("B2:F%i", numRows);
                SpreadSheet::RetrieveTable(Workbook:P_FileName, Parameter:P_PortVesselCategoryCompatibility, DataRange:dataRangeStr, RowsRange:rowRangeStr, ColumnsRange:columnRangeStr);
                
                ! Read PortSailingDraft
                SpreadSheet::SetActiveSheet(Workbook:P_FileName, Name:"PortSailingDraft");
                
                numRows := 1000;
                rowRangeStr := formatString("A3:B%i", numRows);
                columnRangeStr := formatString("C2:F2");
                dataRangeStr := formatString("C3:F%i", numRows);
                SpreadSheet::RetrieveTable(Workbook:P_FileName, Parameter:P_TimeOfTide, DataRange:dataRangeStr, RowsRange:rowRangeStr, ColumnsRange:columnRangeStr);
                
                rowRangeStr := formatString("A3:B%i", numRows);
                columnRangeStr := formatString("H2:K2");
                dataRangeStr := formatString("H3:K%i", numRows);
                SpreadSheet::RetrieveTable(Workbook:P_FileName, Parameter:P_ValueOfTide, DataRange:dataRangeStr, RowsRange:rowRangeStr, ColumnsRange:columnRangeStr);
                
                SpreadSheet::SetActiveSheet(Workbook:P_FileName, Name:"ESS");
                
                numRows := 2500;
                dataRangeStr := formatString("A2:A%i", numRows);
                SpreadSheet::RetrieveSet(Workbook:P_FileName, Set:S_VesselWithESS, Range:dataRangeStr, Mode:'replace');
                
                Spreadsheet::CloseWorkbook(P_FileName, 0);
                
                ! Read Vessel Data
                axll::CloseAllWorkBooks;
                axll::OpenWorkBook(WorkbookFilename : P_FileName );
                axll::SelectSheet(SheetName : "Vessel" );
                
                numRows := val(Last(S_AllVessel));
                
                axll::ReadRawValues(
                	IdentifierReference   : P_NameOfAllVessel,
                	DataRange             : "A2:A" + formatString("%i", numRows),
                	MergeWithExistingData :  0);
                
                axll::ReadRawValues(
                	IdentifierReference   : P_TypeOfVessel,
                	DataRange             : "D2:D" + formatString("%i", numRows),
                	MergeWithExistingData :  0);
                
                axll::ReadRawValues(
                	IdentifierReference   : P_DWTOfVessel,
                	DataRange             : "E2:E" + formatString("%i", numRows),
                	MergeWithExistingData :  0);
                
                axll::ReadRawValues(
                	IdentifierReference   : P_TPCOfVessel,
                	DataRange             : "H2:H" + formatString("%i", numRows),
                	MergeWithExistingData :  0);
                
                axll::ReadRawValues(
                	IdentifierReference   : P_DraftOfVessel,
                	DataRange             : "G2:G" + formatString("%i", numRows),
                	MergeWithExistingData :  0);
                
                axll::ReadRawValues(
                	IdentifierReference   : P_TCEOfVessel,
                	DataRange             : "C2:C" + formatString("%i", numRows),
                	MergeWithExistingData :  0);
                
                axll::ReadRawValues(
                	IdentifierReference   : P_LadenSpeedOfVessel,
                	DataRange             : "K2:K" + formatString("%i", numRows),
                	MergeWithExistingData :  0);
                
                axll::ReadRawValues(
                	IdentifierReference   : P_BallastSpeedOfVessel,
                	DataRange             : "I2:I" + formatString("%i", numRows),
                	MergeWithExistingData :  0);	
                
                axll::ReadRawValues(
                	IdentifierReference   : P_LadenBunkerConsumptionRateOfVessel,
                	DataRange             : "L2:L" + formatString("%i", numRows),
                	MergeWithExistingData :  0);	
                
                axll::ReadRawValues(
                	IdentifierReference   : P_BallastBunkerConsumptionRateOfVessel,
                	DataRange             : "J2:J" + formatString("%i", numRows),
                	MergeWithExistingData :  0);	
                
                axll::ReadRawValues(
                	IdentifierReference   : P_ScaleOfVessel,
                	DataRange             : "F2:F" + formatString("%i", numRows),
                	MergeWithExistingData :  0);
                
                axll::CloseAllWorkBooks;
            }
            StringParameter P_FileName;
            Parameter numRows;
            StringParameter dataRangeStr;
            StringParameter rowRangeStr;
            StringParameter columnRangeStr;
        }
        DeclarationSection Raw_Input_Data {
            Parameter P_PortVesselCategoryCompatibility {
                IndexDomain: (po,vc);
                Range: binary;
            }
            StringParameter P_RequireESSOfCargoPositionString {
                IndexDomain: cap;
            }
            Set S_AllVessel {
                Index: allve;
                Definition: elementrange(1,500,1);
            }
            Set S_VesselWithESS {
                Index: veess;
            }
            StringParameter P_NameOfAllVessel {
                IndexDomain: allve;
            }
            Set S_VesselCategory {
                Index: vc;
            }
            Set S_SNOfTide {
                Index: snt;
                Definition: Data{1, 2};
            }
            StringParameter P_TimeOfTide {
                IndexDomain: (ddt,snt,po);
            }
            Parameter P_ValueOfTide {
                IndexDomain: (ddt,snt,po);
                Unit: m;
            }
        }
        Procedure Prepocess {
            Body: {
                empty Processed_Data;
                empty Derived_Sets_and_Parameters;
                
                !Generate dummy input data
                P_FreightRate(cap | P_FreightRate(cap) = 0[$/ton]) := 10[$/ton];
                P_ETABufferBeforeOfCargoPosition(cap) := 2[day];
                P_ETABufferAfterOfCargoPosition(cap) := 2[day];
                P_ETBBufferBeforeOfCargoPosition(cap) := 2[day];
                P_CargoStemSizeUpVariationPercentage(cap) := 10[%];
                P_CargoStemSizeDownVariationPercentage(cap) := 10[%];
                
                P_AssignedDischargePortOfCargoPosition(cap | Mod(Ord(cap), 10) = 1) := 'BAOSHAN';
                P_AssignedDischargePortOfCargoPosition(cap | Mod(Ord(cap), 10) = 5) := 'HAILI';
                P_TTIPAssignedDischargePortOfCargoPosition(cap | Mod(Ord(cap), 10) = 1 or Mod(Ord(cap), 10) = 5) := 3[day];
                
                P_PossibleDischargePortOfCargoPosition((cap, sn) | Mod(Ord(cap), 10) = 2 and sn = '01') := 'BEILUN';
                P_PossibleDischargePortOfCargoPosition((cap, sn) | Mod(Ord(cap), 10) = 2 and sn = '02') := 'DANDONG';
                P_PossibleDischargePortOfCargoPosition((cap, sn) | Mod(Ord(cap), 10) = 3 and sn = '01') := 'DALIAN';
                P_PossibleDischargePortOfCargoPosition((cap, sn) | Mod(Ord(cap), 10) = 3 and sn = '02') := 'HONG KONG';
                P_PossibleDischargePortOfCargoPosition((cap, sn) | Mod(Ord(cap), 10) = 4 and sn = '01') := 'DBCT';
                P_PossibleDischargePortOfCargoPosition((cap, sn) | Mod(Ord(cap), 10) = 4 and sn = '02') := 'KEMEN';
                P_PossibleDischargePortOfCargoPosition((cap, sn) | Mod(Ord(cap), 10) = 6 and sn = '01') := 'JIANGYIN';
                P_PossibleDischargePortOfCargoPosition((cap, sn) | Mod(Ord(cap), 10) = 6 and sn = '02') := 'DANDONG';
                P_PossibleDischargePortOfCargoPosition((cap, sn) | Mod(Ord(cap), 10) = 7 and sn = '01') := 'KURE';
                P_PossibleDischargePortOfCargoPosition((cap, sn) | Mod(Ord(cap), 10) = 7 and sn = '02') := 'MAILIAO';
                P_PossibleDischargePortOfCargoPosition((cap, sn) | Mod(Ord(cap), 10) = 8 and sn = '01') := 'MAJISHAN';
                P_PossibleDischargePortOfCargoPosition((cap, sn) | Mod(Ord(cap), 10) = 8 and sn = '02') := 'HUANGPU';
                P_PossibleDischargePortOfCargoPosition((cap, sn) | Mod(Ord(cap), 10) = 9 and sn = '01') := 'HUANGPU';
                P_PossibleDischargePortOfCargoPosition((cap, sn) | Mod(Ord(cap), 10) = 9 and sn = '02') := 'HUANGHUA';
                P_PossibleDischargePortOfCargoPosition((cap, sn) | Mod(Ord(cap), 10) = 0 and sn = '01') := 'KUANTAN';
                P_PossibleDischargePortOfCargoPosition((cap, sn) | Mod(Ord(cap), 10) = 0 and sn = '02') := 'PWCS';
                
                P_ProbabilityDischargePortOfCargoPosition((cap, sn) | P_PossibleDischargePortOfCargoPosition(cap, sn) <> '' and Mod(Ord(cap), 10) <> 1 and Mod(Ord(cap), 10) <> 5) := 50[%];
                P_TTIPDischargePortOfCargoPosition((cap, sn) | P_PossibleDischargePortOfCargoPosition(cap, sn) <> '' and Mod(Ord(cap), 10) <> 1 and Mod(Ord(cap), 10) <> 5) := 4[day];
                
                P_FixedVesselOfCargoPosition(cap | Mod(Ord(cap), 20) = 0 and round(Ord(cap)/20) = 1) := First(ve | Ord(ve) = 10);
                P_FixedVesselOfCargoPosition(cap | Mod(Ord(cap), 20) = 0 and round(Ord(cap)/20) = 2) := First(ve | Ord(ve) = 20);
                P_FixedVesselOfCargoPosition(cap | Mod(Ord(cap), 20) = 0 and round(Ord(cap)/20) = 3) := First(ve | Ord(ve) = 30);
                !P_FixedVesselOfCargoPosition('185098') := '001';
                
                P_AssignedVesselOfCargoPosition(cap | Mod(Ord(cap), 22) = 0 and round(Ord(cap)/20) = 1) := First(ve | Ord(ve) = 12);
                P_AssignedVesselOfCargoPosition(cap | Mod(Ord(cap), 22) = 0 and round(Ord(cap)/20) = 2) := First(ve | Ord(ve) = 22);
                P_AssignedVesselOfCargoPosition(cap | Mod(Ord(cap), 22) = 0 and round(Ord(cap)/20) = 3) := First(ve | Ord(ve) = 32);
                
                P_DemurrageRate(ve) := 50 [$/day];
                
                P_CurrentPortOfVessel(ve | Mod(Ord(ve), 10) = 1) := 'CHANGZHOU';
                P_CurrentPortETDOfVessel(ve | Mod(Ord(ve), 10) = 1) := '2018-08-10 00:00:00';
                P_CurrentPortOfVessel(ve | Mod(Ord(ve), 10) = 2) := 'JINGTANG';
                P_CurrentPortETDOfVessel(ve | Mod(Ord(ve), 10) = 2) := '2018-08-24 00:00:00';
                P_CurrentPortOfVessel(ve | Mod(Ord(ve), 10) = 3) := 'KOKURA';
                P_CurrentPortETDOfVessel(ve | Mod(Ord(ve), 10) = 3) := '2018-08-25 00:00:00';
                P_CurrentPortOfVessel(ve | Mod(Ord(ve), 10) = 4) := 'LIUHENG';
                P_CurrentPortETDOfVessel(ve | Mod(Ord(ve), 10) = 4) := '2018-08-26 00:00:00';
                P_CurrentPortOfVessel(ve | Mod(Ord(ve), 10) = 5) := 'PORT KEMBLA';
                P_CurrentPortETDOfVessel(ve | Mod(Ord(ve), 10) = 5) := '2018-08-27 00:00:00';
                P_CurrentPortOfVessel(ve | Mod(Ord(ve), 10) = 6) := 'QINGDAO';
                P_CurrentPortETDOfVessel(ve | Mod(Ord(ve), 10) = 6) := '2018-08-28 00:00:00';
                P_CurrentPortOfVessel(ve | Mod(Ord(ve), 10) = 7) := 'RIZHAO';
                P_CurrentPortETDOfVessel(ve | Mod(Ord(ve), 10) = 7) := '2018-08-29 00:00:00';
                P_CurrentPortOfVessel(ve | Mod(Ord(ve), 10) = 8) := 'SINGAPORE';
                P_CurrentPortETDOfVessel(ve | Mod(Ord(ve), 10) = 8) := '2018-08-30 00:00:00';
                P_CurrentPortOfVessel(ve | Mod(Ord(ve), 10) = 9) := 'WENZHOU';
                P_CurrentPortETDOfVessel(ve | Mod(Ord(ve), 10) = 9) := '2018-08-31 00:00:00';
                P_CurrentPortOfVessel(ve | Mod(Ord(ve), 10) = 0) := 'ZHENJIANG';
                P_CurrentPortETDOfVessel(ve | Mod(Ord(ve), 10) = 0) := '2018-09-01 00:00:00';
                
                P_LadenSpeedOfVessel(ve | P_LadenSpeedOfVessel(ve) <= 0[nm/hour]) := 10[nm/hour];
                P_BallastSpeedOfVessel(ve | P_BallastSpeedOfVessel(ve) <= 0[nm/hour]) := 12[nm/hour];
                P_LadenBunkerConsumptionRateOfVessel(ve | P_LadenBunkerConsumptionRateOfVessel(ve) <= 0[ton/day]) := 35[ton/day];
                P_BallastBunkerConsumptionRateOfVessel(ve | P_BallastBunkerConsumptionRateOfVessel(ve) <= 0[ton/day]) := 42[ton/day];
                P_PortBunkerConsumptionRateOfVessel(ve | P_PortBunkerConsumptionRateOfVessel(ve) <= 0 [ton/day]) := 2[ton/day];
                P_OtherWeightOfVessel(ve) := 100[ton];
                
                P_a(cap, po) := 5[day];
                P_b(cap, po) := 2[day];
                P_c := 1000[$/day];
                P_f := 0.000511[$/ton];
                P_g(cap, po) := 100[$];
                
                !Convert P_RequireESSOfCargoPosition to binary
                for (cap) do
                	if P_RequireESSOfCargoPositionString(cap) = "ESS" then
                		P_RequireESSOfCargoPosition(cap) := 1;
                	else
                		P_RequireESSOfCargoPosition(cap) := 0;
                	endif;	
                endfor;
                
                !Generate S_ESSCapabilityOfVessel
                for (ve) do
                	if P_NameOfVessel(ve) in S_VesselWithESS then
                		P_ESSCapabilityOfVessel(ve) := 1;
                		P_ESSCapabilityOfVesselString(ve) := "ESS";
                	else
                		P_ESSCapabilityOfVessel(ve) := 0;
                		P_ESSCapabilityOfVesselString(ve) := "";
                	endif;
                endfor;
                
                !Generate P_NameofFixedVessel(cap) for display
                P_NameOfFixedVessel(cap) := P_NameOfVessel(P_FixedVesselOfCargoPosition(cap));
                
                !Populate Assigned Discharge Port Of Cargo Position dataset to Possible Discharge Port Of Cargo Position dataset
                P_SNOfPossibleDischargePort := First(sn);
                for (cap | P_AssignedDischargePortOfCargoPosition(cap) <> '') do
                	P_PossibleDischargePortOfCargoPosition(cap, P_SNOfPossibleDischargePort) := P_AssignedDischargePortOfCargoPosition(cap);
                	P_ProbabilityDischargePortOfCargoPosition(cap, P_SNOfPossibleDischargePort) := 100[%];
                	P_TTIPDischargePortOfCargoPosition(cap, P_SNOfPossibleDischargePort) := P_TTIPAssignedDischargePortOfCargoPosition(cap);
                endfor;
                
                !Calculate derived sets and parameters
                P_CargoPositionVesselCompatibility(cap, ve) := 1; !Default value
                
                for (cap, ve) do
                	P_TimeFromCurrentPortToLoadingPort := round((P_Distance(P_CurrentPortOfVessel(ve), P_LoadingPortOfCargoPosition(cap)) / P_BallastSpeedOfVessel(ve)) * (1 + P_SeaFactor));
                	P_VesselETA(cap, ve) := P_CurrentPortETDOfVessel(ve) + P_TimeFromCurrentPortToLoadingPort;
                	!P_VesselETA(cap, ve) := MomentToTimeSlot(
                	!				S_HourlyCalendar, ! (input) a calendar
                	!				P_CurrentPortETDOfVessel(ve), ! (input) an element (time-slot) in the calendar
                	!				P_TimeFromCurrentPortToLoadingPort ! (input) a numerical value
                	!			);
                endfor;
                
                for (cap, ve) do
                	if P_VesselETA(cap, ve) > (P_ETBTargetOfCargoPosition(cap) - P_ETBBufferBeforeOfCargoPosition(cap)) then 
                		P_CargoPositionVesselCompatibility(cap, ve) := 0;
                	endif;
                
                	if P_VesselETA(cap, ve) > P_LaycanEndDateOfCargoPosition(cap) then
                		P_CargoPositionVesselCompatibility(cap, ve) := 0;
                	endif;
                
                	if P_VesselETA(cap, ve) >= P_ETDTargetOfCargoPosition(cap) then
                		P_CargoPositionVesselCompatibility(cap, ve) := 0;
                	endif;
                endfor;
                
                P_TTIPLoadingPort((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1) := P_ETDTargetOfCargoPosition(cap) - P_VesselETA(cap, ve);
                
                !empty P_CategoryOfVessel;
                for (ve) do
                	if 175[kton] > P_DWTOfVessel(ve) > 0[kton] then
                		P_CategoryOfVessel(ve) := 'Small Capesize';
                	elseif 200[kton] > P_DWTOfVessel(ve) >= 175[kton] then
                		P_CategoryOfVessel(ve) := 'Standard Capesize';
                	elseif 220[kton] > P_DWTOfVessel(ve) >= 200[kton] then
                		P_CategoryOfVessel(ve) := 'Newcastlemax';
                	elseif 230[kton] > P_DWTOfVessel(ve) >= 220[kton] then
                		P_CategoryOfVessel(ve) := '210kmt stem VLOC';
                	elseif 260[kton] > P_DWTOfVessel(ve) >= 230[kton] then
                		P_CategoryOfVessel(ve) := '235kmt stem VLOC';		
                	endif;
                endfor;
                P_PortVesselCompatibility(po, ve) := P_PortVesselCategoryCompatibility(po, P_CategoryOfVessel(ve));
                
                !empty P_LoadingPortSailingDraftOnETD, P_TimeDifference, P_MinTimeDifference;
                for (cap) do
                	P_MinHourDifference := -1; !initialization
                	for ((ddt, snt, po) | ddt = S_HourlyToDaily(P_ETDTargetOfCargoPosition(cap)) and po = P_LoadingPortOfCargoPosition(cap)) do
                		if P_TimeOfTide(ddt, snt, po) <> "" then
                			P_HourDifference := abs(TimeSlotCharacteristic(P_ETDTargetOfCargoPosition(cap), 'hour') - Floor(val(P_TimeOfTide(ddt, snt, po))/100));
                			if P_MinHourDifference = -1 then
                				P_MinHourDifference := P_HourDifference;
                				P_LoadingPortSailingDraftOnETD(cap) := P_ValueOfTide(ddt, snt, po);
                			elseif P_HourDifference < P_MinHourDifference then
                				P_MinHourDifference := P_HourDifference;
                				P_LoadingPortSailingDraftOnETD(cap) := P_ValueOfTide(ddt, snt, po);
                			endif;
                		endif;
                	endfor;
                endfor;
                
                !empty P_VesselAllowedDraft, P_VesselMaxAllowedTon;
                for ((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1) do !only consider those satisfy time constraint
                	P_VesselAllowedDraft := min(P_DraftOfVessel(ve), P_LoadingPortSailingDraftOnETD(cap));
                	P_VesselMaxAllowedTon := P_DWTOfVessel(ve) - (P_DraftOfVessel(ve) - P_VesselAllowedDraft) * P_TPCOfVessel(ve);
                
                	if P_CargoStemSizeOfCargoPosition(cap) * (1 - P_CargoStemSizeDownVariationPercentage(cap)) > P_VesselMaxAllowedTon then
                		P_CargoPositionVesselCompatibility(cap, ve) := 0;
                	else
                		P_CargoEstimate(cap, ve) := min(P_CargoStemSizeOfCargoPosition(cap) * (1 + P_CargoStemSizeDownVariationPercentage(cap)), P_VesselMaxAllowedTon) - P_OtherWeightOfVessel(ve);
                	endif;
                endfor;
                
                P_FreightRevenue((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1) := P_FreightRate(cap) * P_CargoEstimate(cap, ve);
                
                P_d := 62;
                P_e := 3.5;
                P_h(cap, po) := 170;
                P_DifferentialFreightRate(cap) := sum(sn | P_PossibleDischargePortOfCargoPosition(cap, sn) <> '', P_ProbabilityDischargePortOfCargoPosition(cap, sn) * 
                	((P_a(cap, P_PossibleDischargePortOfCargoPosition(cap, sn)) * (P_c + P_d * P_f) + P_b(cap, P_PossibleDischargePortOfCargoPosition(cap, sn)) * (P_c + P_e * P_f) + P_g(cap, P_PossibleDischargePortOfCargoPosition(cap, sn))) /
                	P_h(cap, P_PossibleDischargePortOfCargoPosition(cap, sn))));
                
                P_DifferentialFreightRevenue((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1 and (P_TypeOfVessel(ve) = 'OV' or P_TypeOfVessel(ve) = 'TC')) := P_DifferentialFreightRate(cap) * P_CargoEstimate(cap, ve);
                
                for ((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1) do
                	P_TurnTime := 0.5[day];
                
                	!P_LaycanStartDateOfCargoPositionMinusHalfDay := MomentToTimeSlot(
                	!							S_HourlyCalendar, ! (input) a calendar
                	!							P_LaycanStartDateOfCargoPosition(cap), ! (input) an element (time-slot) in the calendar
                	!							-0.5[day] ! (input) a numerical value
                	!						);
                	if P_VesselETA(cap, ve) <= P_LaycanStartDateOfCargoPosition(cap) - 0.5[day] then
                		P_TurnTime := 0[day];
                	elseif P_LaycanStartDateOfCargoPosition(cap) > P_VesselETA(cap, ve) > P_LaycanStartDateOfCargoPosition(cap) - 0.5[day] then
                		P_TurnTime := 0.5[day] - (P_LaycanStartDateOfCargoPosition(cap) - P_VesselETA(cap, ve));
                	endif;
                
                	P_AllowedTime := P_TurnTime + P_CargoEstimate(cap, ve) / P_ScaleOfVessel(ve);
                
                	if (P_TTIPLoadingPort(cap, ve) - P_AllowedTime) >= 0[day] then
                		P_DemurrageRevenueLoadingPort(cap, ve) := (P_TTIPLoadingPort(cap, ve) - P_AllowedTime) * P_DemurrageRate(ve);
                	else
                		P_DemurrageRevenueLoadingPort(cap, ve) := (P_AllowedTime - P_TTIPLoadingPort(cap, ve)) * P_DemurrageRate(ve) / 2;
                	endif;
                endfor;
                
                P_CargoRevenue((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1) := P_FreightRevenue(cap, ve) + P_DifferentialFreightRevenue(cap, ve);
                
                P_BallastTime((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1 and P_BallastSpeedOfVessel(ve) > 0[nm/hour]) := P_Distance(P_CurrentPortOfVessel(ve), P_LoadingPortOfCargoPosition(cap)) / P_BallastSpeedOfVessel(ve);
                
                P_LadenTime((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1 and P_LadenSpeedOfVessel(ve) > 0[nm/hour]) := 
                	sum(sn, P_ProbabilityDischargePortOfCargoPosition(cap, sn) * P_Distance(P_LoadingPortOfCargoPosition(cap), P_PossibleDischargePortOfCargoPosition(cap, sn)) / P_LadenSpeedOfVessel(ve));
                
                P_ExpectTTIPDischargePortOfCargoPosition(cap) := sum(sn, P_ProbabilityDischargePortOfCargoPosition(cap, sn) * P_TTIPDischargePortOfCargoPosition(cap, sn));
                
                P_VoyageLength((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1) := P_BallastTime(cap, ve) * (1 + P_SeaFactor) + P_TTIPLoadingPort(cap, ve) + P_LadenTime(cap, ve) * (1 + P_SeaFactor) + P_ExpectTTIPDischargePortOfCargoPosition(cap);
                
                P_VesselHireCost((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1 and (P_TypeOfVessel(ve) = 'OV' or P_TypeOfVessel(ve) = 'TC')) := P_TCEOfVessel(ve) * P_VoyageLength(cap, ve);
                
                P_VesselBunkeringCost((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1 and (P_TypeOfVessel(ve) = 'OV' or P_TypeOfVessel(ve) = 'TC')) := (P_BallastBunkerConsumptionRateOfVessel(ve) * P_BallastTime(cap, ve) +
                	P_LadenBunkerConsumptionRateOfVessel(ve) * P_LadenTime(cap, ve)) * (1 + P_SeaFactor) * P_BunkerPrice + P_PortBunkerConsumptionRateOfVessel(ve) * (P_TTIPLoadingPort(cap, ve) + P_ExpectTTIPDischargePortOfCargoPosition(cap)) * P_BunkerPrice;
                
                P_VesselDifferentialCost((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1 and (P_TypeOfVessel(ve) = 'OV' or P_TypeOfVessel(ve) = 'TC')) := sum(sn, P_ProbabilityDischargePortOfCargoPosition(cap, sn) * P_TCEOfVessel(ve) *  P_a(cap, P_PossibleDischargePortOfCargoPosition(cap, sn)));
                
                P_VesselType1Cost((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1 and (P_TypeOfVessel(ve) = 'OV' or P_TypeOfVessel(ve) = 'TC')) := P_VesselHireCost(cap, ve) + P_VesselBunkeringCost(cap, ve) + P_VesselDifferentialCost(cap, ve);
                
                P_VesselFreightCost((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1 and (P_TypeOfVessel(ve) = 'VC' or P_TypeOfVessel(ve) = 'COA' or P_TypeOfVessel(ve) = 'CVC')) := P_VesselFreightCostRate(ve) * P_CargoEstimate(cap, ve) * (1 + P_AddcomCostRate);
                
                P_DemurrageCostDischargePort((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1 and (P_TypeOfVessel(ve) = 'VC' or P_TypeOfVessel(ve) = 'COA' or P_TypeOfVessel(ve) = 'CVC')) := sum(sn, 
                	(P_ProbabilityDischargePortOfCargoPosition(cap, sn) * (P_TTIPDischargePortOfCargoPosition(cap, sn) - P_AllowedTimeAtDischargePort(P_PossibleDischargePortOfCargoPosition(cap, sn)))) * P_DemurrageRate(ve));
                
                P_VesselType2Cost((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1 and (P_TypeOfVessel(ve) = 'VC' or P_TypeOfVessel(ve) = 'COA' or P_TypeOfVessel(ve) = 'CVC')) := P_VesselFreightCost(cap, ve) + P_DemurrageCostDischargePort(cap, ve);
                
                !Initialize weights and parameters for penalty
                P_WeightOfETAGapPenalty := 1000[$/day];
                P_WeightOfAssignedVesselChangePenalty := 2000[$]; !2000$/change
                P_WeightOfOutsideETATargetWindowPenalty := 1000[$/day];
                P_WeightOfETABeforeLaycanPenalty := 1000[$/day];
                P_ThresholdOfLongDischargeTTIP := 3[day];
                P_LongDischargeTTIPPenaltyParameter := 1000[$];
                
                !Calculate non-weighted penalty
                P_ETAGapPenalty((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1) := abs((P_VesselETA(cap, ve) - P_ETATargetOfCargoPosition(cap)));
                
                P_AssignedVesselChangePenalty((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1 and P_AssignedVesselOfCargoPosition(cap) <> ve) := 1;
                
                for((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1) do
                	if P_VesselETA(cap, ve) < (P_ETATargetOfCargoPosition(cap) - P_ETABufferBeforeOfCargoPosition(cap)) then
                		P_OutsideETATargetWindowPenalty(cap, ve) := (P_ETATargetOfCargoPosition(cap) - P_ETABufferBeforeOfCargoPosition(cap)) - P_VesselETA(cap, ve);
                	elseif P_VesselETA(cap, ve) > (P_ETATargetOfCargoPosition(cap) + P_ETABufferAfterOfCargoPosition(cap)) then
                		P_OutsideETATargetWindowPenalty(cap, ve) := P_VesselETA(cap, ve) - (P_ETATargetOfCargoPosition(cap) + P_ETABufferAfterOfCargoPosition(cap));
                	else
                		P_OutsideETATargetWindowPenalty(cap, ve) := 0;
                	endif;
                endfor;
                
                for((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1) do
                	if P_VesselETA(cap, ve) < P_LaycanStartDateOfCargoPosition(cap) then
                		P_ETABeforeLaycanPenalty(cap, ve) :=  P_LaycanStartDateOfCargoPosition(cap) - P_VesselETA(cap, ve);
                	else
                		P_ETABeforeLaycanPenalty(cap, ve) := 0;
                	endif;
                endfor;
                
                for((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1) do
                	P_LongDischargeTTIPPenalty(cap, ve) := 0;
                	if (P_ExpectTTIPDischargePortOfCargoPosition(cap) > P_ThresholdOfLongDischargeTTIP and (P_TypeOfVessel(ve) = 'OV' or P_TypeOfVessel(ve) = 'TC')) then
                		P_LongDischargeTTIPPenalty(cap, ve) := P_LongDischargeTTIPPenaltyParameter;
                	endif;
                endfor;
            }
        }
        DeclarationSection Processed_Data {
            ElementParameter P_SNOfPossibleDischargePort {
                Range: S_SN;
            }
            ElementParameter P_CategoryOfVessel {
                IndexDomain: ve;
                Range: S_VesselCategory;
            }
            StringParameter P_ESSCapabilityOfVesselString {
                IndexDomain: ve;
            }
            StringParameter P_NameOfFixedVessel {
                IndexDomain: cap;
            }
            Parameter P_TimeFromCurrentPortToLoadingPort {
                Unit: hour;
            }
            Parameter P_LoadingPortSailingDraftOnETD {
                IndexDomain: cap;
                Unit: m;
            }
            Parameter P_HourDifference;
            Parameter P_MinHourDifference;
            Parameter P_VesselAllowedDraft {
                Unit: m;
            }
            Parameter P_VesselMaxAllowedTon {
                Unit: ton;
            }
            Parameter P_a {
                IndexDomain: (cap,po);
                Unit: day;
            }
            Parameter P_b {
                IndexDomain: (cap,po);
                Unit: day;
            }
            Parameter P_c {
                Unit: $/day;
            }
            Parameter P_d {
                Unit: ton/day;
            }
            Parameter P_e {
                Unit: ton/day;
            }
            Parameter P_f {
                Unit: $/ton;
            }
            Parameter P_g {
                IndexDomain: (cap,po);
                Unit: $;
            }
            Parameter P_h {
                IndexDomain: (cap,po);
                Unit: kton;
            }
            Parameter P_TurnTime {
                Unit: day;
            }
            Parameter P_AllowedTime {
                Unit: day;
            }
            Parameter P_ExpectTTIPDischargePortOfCargoPosition {
                IndexDomain: cap;
                Unit: day;
            }
            Parameter P_BunkerPrice {
                Unit: $/ton;
            }
            Parameter P_AddcomCostRate {
                Unit: %;
                Definition: 3.75;
            }
            Parameter P_AllowedTimeAtDischargePort {
                IndexDomain: po;
                Unit: day;
            }
        }
        Section TestSection {
            Procedure Procedure_test;
            DeclarationSection TestData {
                Parameter hour_test {
                    Unit: hour;
                }
                Parameter test {
                    Definition: val(Last(S_AllVessel));
                }
                Parameter test2 {
                    Range: binary;
                    Definition: First(S_HourlyCalendar) + 100[day] > '2018-02-01' - 3[day];
                }
                StringParameter test3 {
                    Definition: {
                        MomentToTimeSlot(
                        S_HourlyCalendar, ! (input) a calendar
                        First(S_HourlyCalendar) + 3[day], ! (input) an element (time-slot) in the calendar
                        -0.4[day] ! (input) a numerical value
                        )
                    }
                }
                StringParameter test4 {
                    Definition: "B1:B" + formatString("%i", 200);
                }
                Parameter P_Sample1;
            }
        }
    }
    Section Optimization_Model {
        Procedure SolveModel {
            Body: {
                solve M_MathModel;
                empty Raw_results;
                
                P_Assignment(cap, ve) := V_Assignment(cap, ve);
                P_TotalCargoRevenue := V_TotalCargoRevenue;
                P_TotalVesselType1Cost := V_TotalVesselType1Cost;
                P_TotalVesselType2Cost := V_TotalVesselType2Cost;
                P_TotalETAGapPenalty := V_TotalETAGapPenalty;
                P_TotalAssignedVesselChangePenalty := V_TotalAssignedVesselChangePenalty;
                P_TotalOutsideETATargetWindowPenalty := V_TotalOutsideETATargetWindowPenalty;
                P_TotalETABeforeLaycanPenalty := V_TotalETABeforeLaycanPenalty;
                P_TotalLongDischargeTTIPPenalty := V_TotalLongDischargeTTIPPenalty;
                
                dialogmessage("Solve complete");
            }
        }
        Section Model_Inputs {
            DeclarationSection Calendar_Date {
                Calendar S_HourlyCalendar {
                    Index: dt;
                    Unit: hour;
                    BeginDate: S_BeginOfHourlyCalendar;
                    EndDate: S_EndOfHourlyCalendar;
                    TimeslotFormat: "%c%y-%m-%d %H:%M:%S";
                }
                StringParameter S_BeginOfHourlyCalendar {
                    Definition: "2018-08-01 00:00:00";
                }
                StringParameter S_EndOfHourlyCalendar {
                    Definition: "2018-10-31 23:00:00";
                }
                Calendar S_DailyCalendar {
                    Index: ddt;
                    Unit: day;
                    BeginDate: S_BeginOfDailyCalendar;
                    EndDate: S_EndOfDailyCalendar;
                    TimeslotFormat: "%c%y-%m-%d";
                }
                StringParameter S_BeginOfDailyCalendar {
                    Definition: "2018-01-01";
                }
                StringParameter S_EndOfDailyCalendar {
                    Definition: "2018-12-31";
                }
                ElementParameter S_HourlyToDaily {
                    IndexDomain: dt;
                    Range: S_DailyCalendar;
                    Definition: first(ddt | TimeslotCharacteristic(ddt, 'year') = TimeslotCharacteristic(dt, 'year') and TimeslotCharacteristic(ddt, 'month') = TimeslotCharacteristic(dt, 'month') and TimeslotCharacteristic(ddt, 'monthday') = TimeslotCharacteristic(dt, 'monthday'));
                }
            }
            DeclarationSection Quantities_and_Units {
                Quantity SI_Time_Duration {
                    BaseUnit: hour;
                    Conversions: {
                        day   ->hour : #-># * 24,
                        minute->hour : #-># / 60
                    }
                    Comment: "Expresses the value for the duration of periods.";
                }
                Quantity SI_Mass {
                    BaseUnit: ton;
                    Conversions: kton->ton : #-># * 1000;
                    Comment: "Expresses the value for the amount of matter.";
                }
                Quantity SI_Length {
                    BaseUnit: m;
                    Conversions: {
                        cm->m : #-># / 100,
                        nm->m : #-># * 1852
                    }
                    Comment: "Expresses the value of a distance.";
                }
                Quantity Currency {
                    BaseUnit: $;
                }
                Quantity SI_Unitless {
                    BaseUnit: -;
                    Conversions: %->- : #-># / 100;
                    Comment: "Expresses a dimensionless value.";
                }
            }
            Section Input_Sets_and_Parameters {
                DeclarationSection Input_Sets {
                    Set S_CargoPosition {
                        Index: cap;
                    }
                    Set S_Vessel {
                        SubsetOf: S_AllVessel;
                        Index: ve;
                        Definition: {
                            {allve | P_NameOfAllVessel(allve) <> ""} !remove dummy vessel which does not have a name
                        }
                    }
                    Set S_VesselType {
                        Index: vt;
                    }
                    Set S_Port {
                        Index: po, po1, po2;
                    }
                    Set S_Customer {
                        Index: cs;
                    }
                    Set S_FreightMechanism {
                        Index: fm;
                    }
                    Set S_SN {
                        Index: sn;
                        Definition: elementrange(1,50,1);
                    }
                }
                DeclarationSection Input_Parameters {
                    StringParameter P_StatusOfCargoPosition {
                        IndexDomain: cap;
                    }
                    StringParameter P_CustomerOfCargoPosition {
                        IndexDomain: cap;
                    }
                    ElementParameter P_FreightMechanismOfCargoPosition {
                        IndexDomain: cap;
                        Range: S_FreightMechanism;
                    }
                    Parameter P_FreightRate {
                        IndexDomain: cap;
                        Range: nonnegative;
                        Unit: $/ton;
                    }
                    ElementParameter P_LaycanStartDateOfCargoPosition {
                        IndexDomain: cap;
                        Range: S_HourlyCalendar;
                    }
                    ElementParameter P_LaycanEndDateOfCargoPosition {
                        IndexDomain: cap;
                        Range: S_HourlyCalendar;
                    }
                    ElementParameter P_ETATargetOfCargoPosition {
                        IndexDomain: cap;
                        Range: S_HourlyCalendar;
                    }
                    Parameter P_ETABufferBeforeOfCargoPosition {
                        IndexDomain: cap;
                        Range: nonnegative;
                        Unit: day;
                    }
                    Parameter P_ETABufferAfterOfCargoPosition {
                        IndexDomain: cap;
                        Range: nonnegative;
                        Unit: day;
                    }
                    ElementParameter P_ETBTargetOfCargoPosition {
                        IndexDomain: cap;
                        Range: S_HourlyCalendar;
                    }
                    Parameter P_ETBBufferBeforeOfCargoPosition {
                        IndexDomain: cap;
                        Range: nonnegative;
                        Unit: day;
                    }
                    ElementParameter P_ETDTargetOfCargoPosition {
                        IndexDomain: cap;
                        Range: S_HourlyCalendar;
                    }
                    Parameter P_CargoStemSizeOfCargoPosition {
                        IndexDomain: cap;
                        Range: nonnegative;
                        Unit: kton;
                    }
                    Parameter P_CargoStemSizeUpVariationPercentage {
                        IndexDomain: cap;
                        Range: nonnegative;
                        Unit: %;
                    }
                    Parameter P_CargoStemSizeDownVariationPercentage {
                        IndexDomain: cap;
                        Range: nonnegative;
                        Unit: %;
                    }
                    StringParameter P_CargoTierOfCargoPostion {
                        IndexDomain: cap;
                    }
                    ElementParameter P_LoadingPortOfCargoPosition {
                        IndexDomain: cap;
                        Range: S_Port;
                    }
                    ElementParameter P_AssignedDischargePortOfCargoPosition {
                        IndexDomain: cap;
                        Range: S_Port;
                    }
                    Parameter P_TTIPAssignedDischargePortOfCargoPosition {
                        IndexDomain: cap;
                        Range: nonnegative;
                        Unit: day;
                    }
                    ElementParameter P_PossibleDischargePortOfCargoPosition {
                        IndexDomain: (cap,sn);
                        Range: S_Port;
                    }
                    Parameter P_ProbabilityDischargePortOfCargoPosition {
                        IndexDomain: (cap,sn);
                        Range: nonnegative;
                        Unit: %;
                    }
                    Parameter P_TTIPDischargePortOfCargoPosition {
                        IndexDomain: (cap,sn);
                        Range: nonnegative;
                        Unit: day;
                    }
                    Parameter P_RequireESSOfCargoPosition {
                        IndexDomain: cap;
                        Range: binary;
                    }
                    ElementParameter P_FixedVesselOfCargoPosition {
                        IndexDomain: cap;
                        Range: S_Vessel;
                    }
                    ElementParameter P_AssignedVesselOfCargoPosition {
                        IndexDomain: cap;
                        Range: S_Vessel;
                    }
                    ElementParameter P_TypeOfVessel {
                        IndexDomain: ve;
                        Range: S_VesselType;
                    }
                    StringParameter P_NameOfVessel {
                        IndexDomain: ve;
                        Definition: P_NameOfAllVessel(ve);
                    }
                    Parameter P_DWTOfVessel {
                        IndexDomain: ve;
                        Range: nonnegative;
                        Unit: ton;
                    }
                    Parameter P_TPCOfVessel {
                        IndexDomain: ve;
                        Range: nonnegative;
                        Unit: ton/cm;
                    }
                    Parameter P_DraftOfVessel {
                        IndexDomain: ve;
                        Range: nonnegative;
                        Unit: m;
                    }
                    Parameter P_TCEOfVessel {
                        IndexDomain: ve | (P_TypeOfVessel(ve) = "OV" OR P_TypeOfVessel(ve) = "TC");
                        Range: nonnegative;
                        Unit: $/day;
                    }
                    Parameter P_DemurrageRate {
                        IndexDomain: ve;
                        Range: nonnegative;
                        Unit: $/day;
                    }
                    Parameter P_VesselFreightCostRate {
                        IndexDomain: ve | (P_TypeOfVessel(ve) = "VC" OR P_TypeOfVessel(ve) = "COA" OR P_TypeOfVessel(ve) = "CVC");
                        Range: nonnegative;
                        Unit: $/ton;
                    }
                    ElementParameter P_CurrentPortOfVessel {
                        IndexDomain: ve;
                        Range: S_Port;
                    }
                    ElementParameter P_CurrentPortETDOfVessel {
                        IndexDomain: ve;
                        Range: S_HourlyCalendar;
                    }
                    Parameter P_LadenSpeedOfVessel {
                        IndexDomain: ve;
                        Range: nonnegative;
                        Unit: nm/hour;
                    }
                    Parameter P_BallastSpeedOfVessel {
                        IndexDomain: ve;
                        Range: nonnegative;
                        Unit: nm/hour;
                    }
                    Parameter P_LadenBunkerConsumptionRateOfVessel {
                        IndexDomain: ve;
                        Range: nonnegative;
                        Unit: ton/day;
                    }
                    Parameter P_BallastBunkerConsumptionRateOfVessel {
                        IndexDomain: ve;
                        Range: nonnegative;
                        Unit: ton/day;
                    }
                    Parameter P_PortBunkerConsumptionRateOfVessel {
                        IndexDomain: ve;
                        Range: nonnegative;
                        Unit: ton/day;
                    }
                    Parameter P_ScaleOfVessel {
                        IndexDomain: ve;
                        Range: nonnegative;
                        Unit: ton/day;
                    }
                    Parameter P_ESSCapabilityOfVessel {
                        IndexDomain: ve;
                        Range: binary;
                    }
                    Parameter P_OtherWeightOfVessel {
                        IndexDomain: ve;
                        Range: nonnegative;
                        Unit: ton;
                    }
                    ElementParameter P_OwnerLaycanStartDate {
                        IndexDomain: (cap,ve);
                        Range: S_HourlyCalendar;
                    }
                    ElementParameter P_OwnerLaycanEndDate {
                        IndexDomain: (cap,ve);
                        Range: S_HourlyCalendar;
                    }
                    Parameter P_Distance {
                        IndexDomain: (po1,po2);
                        Range: nonnegative;
                        Unit: nm;
                    }
                    Parameter P_SeaFactor {
                        Unit: %;
                        Definition: 5;
                    }
                }
            }
            DeclarationSection Derived_Sets_and_Parameters {
                Parameter P_CargoPositionVesselCompatibility {
                    IndexDomain: (cap,ve);
                    Range: binary;
                }
                ElementParameter P_VesselETA {
                    IndexDomain: (cap,ve);
                    Range: S_HourlyCalendar;
                }
                Parameter P_TTIPLoadingPort {
                    IndexDomain: (cap,ve);
                    Unit: day;
                }
                Parameter P_PortVesselCompatibility {
                    IndexDomain: (po,ve);
                    Range: binary;
                }
                Parameter P_CargoEstimate {
                    IndexDomain: (cap,ve);
                    Unit: ton;
                }
                Parameter P_FreightRevenue {
                    IndexDomain: (cap,ve);
                    Unit: $;
                }
                Parameter P_DifferentialFreightRate {
                    IndexDomain: cap;
                    Unit: $/ton;
                }
                Parameter P_DifferentialFreightRevenue {
                    IndexDomain: (cap,ve);
                    Unit: $;
                }
                Parameter P_DemurrageRevenueLoadingPort {
                    IndexDomain: (cap,ve);
                    Unit: $;
                }
                Parameter P_CargoRevenue {
                    IndexDomain: (cap,ve);
                    Unit: $;
                }
                Parameter P_BallastTime {
                    IndexDomain: (cap,ve);
                    Unit: day;
                }
                Parameter P_LadenTime {
                    IndexDomain: (cap,ve);
                    Unit: day;
                }
                Parameter P_VoyageLength {
                    IndexDomain: (cap,ve);
                    Unit: day;
                }
                Parameter P_VesselHireCost {
                    IndexDomain: (cap,ve) | (P_TypeOfVessel(ve) = "OV" OR P_TypeOfVessel(ve) = "TC");
                    Unit: $;
                }
                Parameter P_VesselBunkeringCost {
                    IndexDomain: (cap,ve) | (P_TypeOfVessel(ve) = "OV" OR P_TypeOfVessel(ve) = "TC");
                    Unit: $;
                }
                Parameter P_VesselDifferentialCost {
                    IndexDomain: (cap,ve) | (P_TypeOfVessel(ve) = "OV" OR P_TypeOfVessel(ve) = "TC");
                    Unit: $;
                }
                Parameter P_VesselType1Cost {
                    IndexDomain: (cap,ve) | (P_TypeOfVessel(ve) = "OV" OR P_TypeOfVessel(ve) = "TC");
                    Unit: $;
                }
                Parameter P_VesselFreightCost {
                    IndexDomain: (cap,ve) | (P_TypeOfVessel(ve) = "VC" OR P_TypeOfVessel(ve) = "COA" OR P_TypeOfVessel(ve) = "CVC");
                    Unit: $;
                }
                Parameter P_DemurrageCostDischargePort {
                    IndexDomain: (cap,ve) | (P_TypeOfVessel(ve) = "VC" or P_TypeOfVessel(ve) = "COA" or P_TypeOfVessel(ve) = "CVC");
                    Unit: $;
                }
                Parameter P_VesselType2Cost {
                    IndexDomain: (cap,ve) | (P_TypeOfVessel(ve) = "VC" OR P_TypeOfVessel(ve) = "COA" OR P_TypeOfVessel(ve) = "CVC");
                    Unit: $;
                }
                Parameter P_ETAGapPenalty {
                    IndexDomain: (cap,ve);
                    Unit: day;
                }
                Parameter P_AssignedVesselChangePenalty {
                    IndexDomain: (cap,ve);
                }
                Parameter P_OutsideETATargetWindowPenalty {
                    IndexDomain: (cap,ve);
                    Unit: day;
                }
                Parameter P_ETABeforeLaycanPenalty {
                    IndexDomain: (cap,ve);
                    Unit: day;
                }
                Parameter P_LongDischargeTTIPPenalty {
                    IndexDomain: (cap,ve) | (P_TypeOfVessel(ve) = "OV" OR P_TypeOfVessel(ve) = "TC");
                    Unit: $;
                }
            }
        }
        Section Model_Decision_Variables {
            DeclarationSection Decision_Variables {
                Variable V_Assignment {
                    IndexDomain: (cap,ve);
                    Range: binary;
                }
            }
        }
        Section Model_Constraints {
            Constraint C_FixedVessel {
                IndexDomain: (cap)| P_FixedVesselOfCargoPosition(cap) <> '';
                Definition: V_Assignment(cap, P_FixedVesselOfCargoPosition(cap)) = 1;
            }
            Constraint C_CargoPositionVesselCompatibility {
                IndexDomain: (cap,ve) | P_CargoPositionVesselCompatibility(cap, ve) = 0 and P_FixedVesselOfCargoPosition(cap) = '';
                Definition: V_Assignment(cap, ve) = 0;
            }
            Constraint C_PortVesselCompatibility {
                IndexDomain: (cap,ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1 and sum(sn, P_PortVesselCompatibility(P_PossibleDischargePortOfCargoPosition(cap, sn), ve)) = 0 and P_FixedVesselOfCargoPosition(cap) = '';
                Definition: V_Assignment(cap, ve) = 0;
            }
            Constraint C_OneCargoPositionToOneVessel {
                IndexDomain: cap;
                Definition: sum(ve | P_CargoPositionVesselCompatibility(cap, ve) = 1, V_Assignment(cap, ve)) <= 1;
            }
            Constraint C_OneVesselToOneCargoPosition {
                IndexDomain: ve;
                Definition: sum(cap | P_CargoPositionVesselCompatibility(cap, ve) = 1, V_Assignment(cap, ve)) <= 1;
            }
            Constraint C_NoVesselETAAfterCargoPositionETBWithBuffer {
                IndexDomain: cap;
                Definition: sum(ve | P_CargoPositionVesselCompatibility(cap, ve) = 1 and P_VesselETA(cap, ve) > P_ETBTargetOfCargoPosition(cap) - P_ETBBufferBeforeOfCargoPosition(cap), V_Assignment(cap, ve)) = 0;
            }
            Constraint C_NoVesselETAAfterCargoPositionLaycanEnd {
                IndexDomain: cap;
                Definition: sum(ve | P_CargoPositionVesselCompatibility(cap, ve) = 1 and P_VesselETA(cap, ve) > P_LaycanEndDateOfCargoPosition(cap), V_Assignment(cap, ve)) = 0;
            }
            Constraint C_CargoPositionESSRequirement {
                IndexDomain: (cap,ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1 and P_RequireESSOfCargoPosition(cap) = 1 and P_ESSCapabilityOfVessel(ve) = 0 and P_FixedVesselOfCargoPosition(cap) = '';
                Definition: V_Assignment(cap, ve) = 0;
            }
            Constraint C_SumCargoRevenue {
                Unit: $;
                Definition: V_TotalCargoRevenue = sum((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1, P_CargoRevenue(cap, ve) * V_Assignment(cap, ve));
            }
            Constraint C_SumVesselType1Cost {
                Unit: $;
                Definition: {
                    V_TotalVesselType1Cost = sum((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1 and (P_TypeOfVessel(ve) = 'OV' or P_TypeOfVessel(ve) = 'TC'),
                    			P_VesselType1Cost(cap, ve) * V_Assignment(cap, ve))
                }
            }
            Constraint C_SumVesselType2Cost {
                Unit: $;
                Definition: {
                    V_TotalVesselType2Cost = sum((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1 and (P_TypeOfVessel(ve) = 'VC' or P_TypeOfVessel(ve) = 'COA' or P_TypeOfVessel(ve) = 'CVC'),
                    			P_VesselType2Cost(cap, ve) * V_Assignment(cap, ve))
                }
            }
            Constraint C_SumETAGapPenalty {
                Unit: day;
                Definition: V_TotalETAGapPenalty = sum((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1, P_ETAGapPenalty(cap, ve) * V_Assignment(cap, ve));
            }
            Constraint C_SumAssignedVesselChangePenalty {
                Definition: V_TotalAssignedVesselChangePenalty = sum((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1, P_AssignedVesselChangePenalty(cap, ve) * V_Assignment(cap, ve));
            }
            Constraint C_SumOutsideETATargetWindowPenalty {
                Unit: day;
                Definition: V_TotalOutsideETATargetWindowPenalty = sum((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1, P_OutsideETATargetWindowPenalty(cap, ve) * V_Assignment(cap, ve));
            }
            Constraint C_SumETABeforeLaycanPenalty {
                Unit: day;
                Definition: V_TotalETABeforeLaycanPenalty = sum((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1, P_ETABeforeLaycanPenalty(cap, ve) * V_Assignment(cap, ve));
            }
            Constraint C_SumLongDischargeTTIPPenalty {
                Unit: $;
                Definition: V_TotalLongDischargeTTIPPenalty = sum((cap, ve) | P_CargoPositionVesselCompatibility(cap, ve) = 1, P_LongDischargeTTIPPenalty(cap, ve) * V_Assignment(cap, ve));
            }
            Constraint C_Objective {
                Unit: $;
                Definition: {
                    V_Objective = 	V_TotalCargoRevenue
                    		- V_TotalVesselType1Cost
                    		- V_TotalVesselType2Cost
                    		- P_WeightOfETAGapPenalty * V_TotalETAGapPenalty
                    		- P_WeightOfAssignedVesselChangePenalty * V_TotalAssignedVesselChangePenalty
                    		- P_WeightOfOutsideETATargetWindowPenalty * V_TotalOutsideETATargetWindowPenalty
                    		- P_WeightOfETABeforeLaycanPenalty * V_TotalETABeforeLaycanPenalty
                    		- V_TotalLongDischargeTTIPPenalty
                }
            }
        }
        Section Model_Objectives {
            Variable V_TotalCargoRevenue {
                Range: free;
                Unit: $;
            }
            Variable V_TotalVesselType1Cost {
                Range: free;
                Unit: $;
            }
            Variable V_TotalVesselType2Cost {
                Range: free;
                Unit: $;
            }
            Variable V_TotalETAGapPenalty {
                Range: free;
                Unit: day;
            }
            Variable V_TotalAssignedVesselChangePenalty {
                Range: free;
            }
            Variable V_TotalOutsideETATargetWindowPenalty {
                Range: free;
                Unit: day;
            }
            Variable V_TotalETABeforeLaycanPenalty {
                Range: free;
                Unit: day;
            }
            Variable V_TotalLongDischargeTTIPPenalty {
                Range: free;
                Unit: $;
            }
            Variable V_Objective {
                Range: free;
                Unit: $;
            }
            DeclarationSection Model_Weights {
                Parameter P_WeightOfETAGapPenalty {
                    Unit: $/day;
                }
                Parameter P_WeightOfAssignedVesselChangePenalty {
                    Unit: $;
                }
                Parameter P_WeightOfOutsideETATargetWindowPenalty {
                    Unit: $/day;
                }
                Parameter P_WeightOfETABeforeLaycanPenalty {
                    Unit: $/day;
                }
                Parameter P_ThresholdOfLongDischargeTTIP {
                    Unit: day;
                }
                Parameter P_LongDischargeTTIPPenaltyParameter {
                    Range: nonnegative;
                    Unit: $;
                }
            }
        }
        Section Mathematical_Model {
            Set S_ModelVariables {
                SubsetOf: AllVariables;
                Definition: {
                    data{
                    	V_Assignment,
                    	V_TotalCargoRevenue,
                    	V_TotalVesselType1Cost,
                    	V_TotalVesselType2Cost,
                    	V_TotalETAGapPenalty,
                    	V_TotalAssignedVesselChangePenalty,
                    	V_TotalOutsideETATargetWindowPenalty,
                    	V_TotalETABeforeLaycanPenalty,
                    	V_TotalLongDischargeTTIPPenalty,
                    	V_Objective
                    	}
                }
            }
            Set S_ModelConstraints {
                SubsetOf: AllConstraints;
                Definition: {
                    data{
                    	C_FixedVessel,
                    	C_CargoPositionVesselCompatibility,
                    	C_PortVesselCompatibility,
                    	C_OneCargoPositionToOneVessel,
                    	C_OneVesselToOneCargoPosition,
                    	C_NoVesselETAAfterCargoPositionETBWithBuffer,
                    	C_NoVesselETAAfterCargoPositionLaycanEnd,
                    	C_CargoPositionESSRequirement,
                    	C_SumCargoRevenue,
                    	C_SumVesselType1Cost,
                    	C_SumVesselType2Cost,
                    	C_SumETAGapPenalty,
                    	C_SumAssignedVesselChangePenalty,
                    	C_SumOutsideETATargetWindowPenalty,
                    	C_SumETABeforeLaycanPenalty,
                    	C_SumLongDischargeTTIPPenalty,
                    	C_Objective
                    	}
                }
            }
            MathematicalProgram M_MathModel {
                Objective: V_Objective;
                Direction: maximize;
                Constraints: S_ModelConstraints;
                Variables: S_ModelVariables;
                Type: MIP;
            }
        }
    }
    Section Results {
        DeclarationSection Raw_results {
            Parameter P_Assignment {
                IndexDomain: (cap,ve);
                Range: binary;
            }
            Parameter P_TotalCargoRevenue {
                Unit: $;
            }
            Parameter P_TotalVesselType1Cost {
                Unit: $;
            }
            Parameter P_TotalVesselType2Cost {
                Unit: $;
            }
            Parameter P_TotalETAGapPenalty {
                Unit: day;
            }
            Parameter P_TotalAssignedVesselChangePenalty;
            Parameter P_TotalOutsideETATargetWindowPenalty {
                Unit: day;
            }
            Parameter P_TotalETABeforeLaycanPenalty {
                Unit: day;
            }
            Parameter P_TotalLongDischargeTTIPPenalty {
                Unit: $;
            }
        }
        Procedure PostProcess {
            Body: {
                empty Results_for_Display;
                
                P_FinalAssignedVesselOfCargoPosition(cap) := First(ve | P_Assignment(cap, ve) = 1);
                P_NameOfFinalAssignedVessel(cap) := P_NameOfVessel(P_FinalAssignedVesselOfCargoPosition(cap));
                P_TypeOfFinalAssignedVessel(cap) := P_TypeOfVessel(P_FinalAssignedVesselOfCargoPosition(cap));
                
                
                P_TotalVesselCost := P_TotalVesselType1Cost + P_TotalVesselType2Cost;
                P_TotalMargin := P_TotalCargoRevenue - P_TotalVesselCost;
                P_NumberOfLongDischargeTTIP := round(P_TotalLongDischargeTTIPPenalty / P_LongDischargeTTIPPenaltyParameter);
            }
        }
        DeclarationSection Results_for_Display {
            ElementParameter P_FinalAssignedVesselOfCargoPosition {
                IndexDomain: cap;
                Range: S_Vessel;
            }
            StringParameter P_NameOfFinalAssignedVessel {
                IndexDomain: cap;
            }
            ElementParameter P_TypeOfFinalAssignedVessel {
                IndexDomain: cap;
                Range: S_VesselType;
            }
            Parameter P_TotalVesselCost {
                Unit: $;
            }
            Parameter P_TotalMargin {
                Unit: $;
            }
            Parameter P_NumberOfLongDischargeTTIP;
        }
    }
    Procedure ProcessesinOneGo {
        Body: {
            ReadInput;
            Prepocess;
            SolveModel;
            PostProcess;
        }
    }
    Procedure MainInitialization {
        Comment: "Add initialization statements here that do NOT require any library being initialized already.";
    }
    Procedure PostMainInitialization {
        Comment: {
            "Add initialization statements here that require that the libraries are already initialized properly,
            or add statements that require the Data Management module to be initialized."
        }
    }
    Procedure MainExecution;
    Procedure PreMainTermination {
        Body: {
            return DataManagementExit();
        }
        Comment: {
            "Add termination statements here that require all libraries to be still alive.
            Return 1 if you allow the termination sequence to continue.
            Return 0 if you want to cancel the termination sequence."
        }
    }
    Procedure MainTermination {
        Body: {
            return 1;
        }
        Comment: {
            "Add termination statements here that do not require all libraries to be still alive.
            Return 1 to allow the termination sequence to continue.
            Return 0 if you want to cancel the termination sequence.
            It is recommended to only use the procedure PreMainTermination to cancel the termination sequence and let this procedure always return 1."
        }
    }
}
